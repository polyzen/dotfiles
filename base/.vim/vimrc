" {{{ General
let g:tar_cmd = 'bsdtar' " Workaround 'unknown extended header' error
set guifont=Fantasque\ Sans\ Mono\ 12
set guioptions-=e " Use 'a non-GUI tab pages line'
set hidden        " Allow hidden buffers
set history=500
set laststatus=2  " Always show status line
set showtabline=2 " .. and tab pages line
set mouse=a       " Enable mouse (esp. for balloons and scrolling in popups)
set ttymouse=sgr  " .. also in 'terminals that emit SGR-styled mouse reporting'
set noshowmode    " Do not echo the mode, status line will display it instead
set wildmenu      " Enable command-line completion menu
set wildmode=longest:full,full " .. with incremental completions

" Search
set hlsearch      " Highlight matches
set incsearch     " .. while typing
set ignorecase    " Ignore case
set smartcase     " .. unless 'the search pattern contains upper case characters'
set shortmess-=S  " 'Show search count message when searching'

" True colors
scriptencoding utf-8
let &t_8f = '[38;2;%lu;%lu;%lum'
let &t_8b = '[48;2;%lu;%lu;%lum'
scriptencoding
set termguicolors

" Two-space indentation
set autoindent
set expandtab
set shiftwidth=2
set softtabstop=2
" }}}

" {{{ Plugin management
call plug#begin('~/.vim/plugged')
Plug 'prabirshrestha/asyncomplete.vim'
Plug 'prabirshrestha/asyncomplete-lsp.vim'
Plug 'ntpeters/vim-better-whitespace'
Plug 'moll/vim-bbye'
Plug 'liuchengxu/vim-clap', { 'do': ':call clap#installer#build_python_dynamic_module()' }
let g:clap_enable_icon = 1

Plug 'rhysd/clever-f.vim'
Plug 'wellle/context.vim'
Plug 'romainl/vim-cool'
Plug 'rbong/vim-crystalline'
let g:crystalline_statusline_fn = 'StatusLine'
let g:crystalline_tabline_fn = 'TabLine'
let g:crystalline_theme = 'gruvbox'

Plug 'ap/vim-css-color'
Plug 'tpope/vim-dispatch'
Plug 'junegunn/vim-easy-align'
nmap ga <Plug>(EasyAlign)
xmap ga <Plug>(EasyAlign)

Plug 'haya14busa/vim-edgemotion'
map <C-j> <Plug>(edgemotion-j)
map <C-k> <Plug>(edgemotion-k)

Plug 'editorconfig/editorconfig-vim'
Plug 'Konfekt/FastFold'
Plug 'Jorengarenar/fauxClip'
Plug 'lambdalisue/fern.vim' | Plug 'lambdalisue/fern-renderer-nerdfont.vim'
let g:fern#renderer = 'nerdfont'

Plug 'lambdalisue/glyph-palette.vim'
augroup my_glyph_palette
  autocmd! *
  autocmd FileType fern call glyph_palette#apply()
augroup END

Plug 'junegunn/goyo.vim' | Plug 'junegunn/limelight.vim'
Plug 'gruvbox-community/gruvbox'
let g:gruvbox_italic = 1

Plug 'machakann/vim-highlightedyank'
Plug 'Yggdroot/indentLine'
let g:indentLine_char_list = ['|', 'Â¦', 'â”†', 'â”Š']
let g:indentLine_bufTypeExclude = ['help']

Plug 'andymass/vim-matchup'
let g:matchup_matchparen_offscreen = {}

if executable('code-minimap')
  Plug 'wfxr/minimap.vim'
endif
Plug 'markwu/vim-mrufiles'
Plug 'lambdalisue/nerdfont.vim'
Plug 'junegunn/vim-peekaboo'
Plug 'blueyed/vim-qf_resize'
Plug 'itchyny/vim-qfedit'
Plug 'unblevable/quick-scope'
Plug 'AndrewRadev/quickpeek.vim'
Plug 'tpope/vim-repeat'
Plug 'rhysd/reply.vim'
Plug 'tpope/vim-rsi'
Plug 'matthew-brett/vim-rst-sections'
Plug 'arzg/vim-rust-syntax-ext'
Plug 'psliwka/vim-smoothie'
Plug 'justinmk/vim-sneak'
Plug 'AndrewRadev/splitjoin.vim'
Plug 'aymericbeaumet/vim-symlink'
Plug 'dhruvasagar/vim-table-mode'
Plug 'tmux-plugins/vim-tmux-focus-events'
Plug 'cespare/vim-toml'
Plug 'markonm/traces.vim'
Plug 'andymass/vim-tradewinds'
Plug 'tpope/vim-unimpaired'
Plug 'dhruvasagar/vim-zoom'
let g:zoom#statustext = "\uf848 "

" Git
if executable('git')
  Plug 'hotwatermorning/auto-git-diff'
  Plug 'rhysd/committia.vim'
  Plug 'tpope/vim-fugitive' | Plug 'junegunn/gv.vim'
  Plug 'shumphrey/fugitive-gitlab.vim' | Plug 'tpope/vim-rhubarb'
  Plug 'rhysd/git-messenger.vim'
  Plug 'airblade/vim-gitgutter'
  let g:gitgutter_preview_win_floating = 1
endif

" Python
Plug 'kalekundert/vim-coiled-snake'
if has('python3')
  Plug 'SirVer/ultisnips' | Plug 'honza/vim-snippets'
  Plug 'prabirshrestha/asyncomplete-ultisnips.vim'
  augroup asyncomplete_ultisnips
    autocmd!
    autocmd User asyncomplete_setup call asyncomplete#register_source(asyncomplete#sources#ultisnips#get_source_options({
      \ 'name': 'ultisnips',
      \ 'allowlist': ['*'],
      \ 'completor': function('asyncomplete#sources#ultisnips#completor'),
      \ }))
  augroup END
endif

" LSP
if executable('ccls')
  Plug 'm-pilia/vim-ccls' | Plug 'jackguo380/vim-lsp-cxx-highlight'
  augroup lsp_ccls
    autocmd!
    autocmd User lsp_setup call lsp#register_server({
      \ 'name': 'ccls',
      \ 'cmd': {server_info->['ccls']},
      \ 'root_uri': {server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_file_directory(lsp#utils#get_buffer_path(), 'compile_commands.json'))},
      \ 'initialization_options': {
      \   'highlight': { 'lsRanges' : v:true },
      \ },
      \ 'whitelist': ['c', 'cpp', 'objc', 'objcpp', 'cc'],
      \ })
  augroup END
endif
if executable('efm-langserver')
  Plug 'tsuyoshicho/vim-efm-langserver-settings'
endif
Plug 'prabirshrestha/vim-lsp'
let g:lsp_diagnostics_float_cursor = 1
let g:lsp_semantic_enabled = 1
set foldmethod=expr
  \ foldexpr=lsp#ui#vim#folding#foldexpr()
  \ foldtext=lsp#ui#vim#folding#foldtext()

Plug 'mattn/vim-lsp-settings'
let g:lsp_settings = {
  \  'clangd': { 'disabled': v:true },
  \  'efm-langserver': { 'disabled': v:false }
  \}
let g:lsp_settings_enable_suggestions = 0

Plug 'liuchengxu/vista.vim'
let g:vista_default_executive = 'vim_lsp'

Plug 'hrsh7th/vim-vsnip'
Plug 'hrsh7th/vim-vsnip-integ'
imap <expr> <C-j>   vsnip#expandable() ? '<Plug>(vsnip-expand)'         : '<C-j>'
smap <expr> <C-j>   vsnip#expandable() ? '<Plug>(vsnip-expand)'         : '<C-j>'
imap <expr> <C-l>   vsnip#available(1) ? '<Plug>(vsnip-expand-or-jump)' : '<C-l>'
smap <expr> <C-l>   vsnip#available(1) ? '<Plug>(vsnip-expand-or-jump)' : '<C-l>'
imap <expr> <Tab>   vsnip#jumpable(1)  ? '<Plug>(vsnip-jump-next)'      : '<Tab>'
smap <expr> <Tab>   vsnip#jumpable(1)  ? '<Plug>(vsnip-jump-next)'      : '<Tab>'
imap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)'      : '<S-Tab>'
smap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)'      : '<S-Tab>'
call plug#end()
colorscheme gruvbox
" }}}

" {{{ Status and tab pages lines
function! StatusLine(current, width)
  let l:s = ''

  if a:current
    let l:s .= crystalline#mode() . crystalline#right_mode_sep('')
  else
    let l:s .= '%#CrystallineInactive#'
  endif
  let l:s .= ' %t%h%w%m%r '
  if a:current
    let l:s .= '%{zoom#statusline()}' . crystalline#right_sep('', 'Fill')
    if a:width > 80
      let l:s .= '%{MyGitStatusline()}'
    endif
  endif

  let l:s .= '%='
  if a:current
    if a:width > 80
      let l:s .= '%{get(b:, "vista_nearest_method_or_function", "")} '
    endif
    let l:s .= crystalline#left_sep('', 'Fill') . ' %{&paste ?"PASTE ":""}%{&spell?"SPELL ":""}'
    let l:s .= crystalline#left_mode_sep('')
  endif
  if a:width > 80
    let l:s .= ' %{strlen(&filetype) ? &filetype . " " . nerdfont#find() . " " : ""}'
    let l:s .= '[%{&fenc!=#""?&fenc:&enc}][%{nerdfont#fileformat#find()} ]'
  else
  endif
  let l:s .= ' %l/%L %c%V %P '

  return l:s
endfunction

function! TabLabel(buf, max_width) abort
  let [l:left, l:name, l:short_name, l:right] = crystalline#default_tablabel_parts(a:buf, a:max_width)
  return l:left . l:short_name . ' ' . nerdfont#find(l:name) . (l:right ==# ' ' ? '' : ' ') . l:right
endfunction

function! TabLine() abort
  return crystalline#bufferline(0, 0, 0, 1, 'TabLabel', crystalline#default_tabwidth() + 3)
endfunction

function! MyGitStatusline() abort
  if &modifiable && exists('b:git_dir')
    let l:out = ''
    if g:gitgutter_enabled
      let [l:added, l:modified, l:removed] = GitGutterGetHunkSummary()
      let l:out .= printf('+%d ~%d -%d ', l:added, l:modified, l:removed)
    endif
    let l:branch = FugitiveHead()
    let l:out .= l:branch !=# '' ? "\ue0a0 " . l:branch : ''
    return !empty(l:out) ? ' ' . l:out : ''
  else
    return ''
  endif
endfunction
" }}}

" vim: fdm=marker
